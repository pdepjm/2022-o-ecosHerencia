import ecosistemas.*

describe "tests de Ecosistemas (parte 1)" {
	const golondrina = new Especie(coeficiente = 15, formaLocomocion = volar, pesoReferencia = 2)
	const mejillon = new Especie(coeficiente = 24, pesoReferencia = 0.5)
	const hormiga = new Especie(coeficiente = 30, formaLocomocion = correr, pesoReferencia = 0.1)
	const plantusPotus = new Especie()
	
	const pepita = new Animal(especie = golondrina, peso = 1)
	const lara = new Animal(especie = golondrina, peso = 10)
	const almejita = new Animal(especie = mejillon, peso = 0.1)
	const atomica = new Animal(especie = hormiga, peso = 0.01)
	
	const palmera = new Planta(altura = 9, especie = plantusPotus )
	const arbusto = new Planta(altura = 3, especie = plantusPotus)
	const arbol1 = new Planta(altura = 5000, especie = plantusPotus)
	const arbol2 = new Planta(altura = 6000, especie = plantusPotus)
	const arbol3 = new Planta(altura = 4000, especie = plantusPotus)
	const rosa = new Planta(altura = 2, especie = plantusPotus)
	
	const llanura = new Habitat(seresVivos = [pepita, almejita, palmera, arbusto])
	const desierto = new Habitat(seresVivos = [atomica, lara, rosa, arbusto])
	const bosque = new Habitat(seresVivos = [atomica, lara, arbol1, arbol2, arbol3, arbol1, arbol2, arbol3])	
	
	method initialize(){
		reserva.agregarHabitat(llanura)
		reserva.agregarHabitat(desierto)
		reserva.agregarHabitat(bosque)	
	}
	
	test "Un habitat está en equilibrio cuando la cant de ejemplares grandes es menor a ⅓ de la cantidad de pequeños y hay alguno vivo"{ 
		assert.that(llanura.estaEnEquilibrio())
	}
	test "Un habitat no está en equilibrio si no hay ningún ejemplar vivo"{
		llanura.incendio()
		assert.notThat(llanura.estaEnEquilibrio())
	}
	test "Un habitat no está en equilibrio cuando la cant de ejemplares grandes es mayor a ⅓ de la cantidad de pequeños"{
		assert.notThat(desierto.estaEnEquilibrio())
	}
	
	test "Si un habitat se incendia y todos sus ejemplares cumplen condición para morir, no queda ninguno vivo"{
		llanura.incendio()
		assert.notThat(llanura.hayAlgunoVivo())
	}
	test "Si un habitat se incendia y por lo menos uno de sus ejemplares cumple condición para vivir, queda alguno vivo"{
		desierto.incendio()
		assert.that(desierto.hayAlgunoVivo())
	}
	
	test "Habitat de mayor biomasa"{
		assert.equals(bosque, reserva.mayorBiomasa())
	}
	test "Cantidad total de biomasa"{
		assert.equals(347.40043, reserva.totalBiomasa())
	}
	test "Especie que esta en todos los habitats"{
		assert.that(reserva.especieEnTodas(plantusPotus))
	}
}

 
describe "Granjas"{
	const golondrina = new Especie(coeficiente = 15, formaLocomocion = volar, pesoReferencia = 2)
	const mejillon = new Especie(coeficiente = 24, pesoReferencia = 0.5)
	const hormiga = new Especie(coeficiente = 30, formaLocomocion = correr, pesoReferencia = 0.1)
	const ganado = new Especie(coeficiente = 24, pesoReferencia = 0.5)  
	const plantusPotus = new Especie()
	
	const pepita = new Animal(especie = golondrina, peso = 1)
	const almejita = new Animal(especie = mejillon, peso = 0.1)
	
	const palmera = new Planta(altura = 9, especie = plantusPotus)
	const arbusto = new Planta(altura = 3, especie = plantusPotus)
	
	const granja = new Granja(seresVivos =  [pepita, almejita, palmera, arbusto])
	const sabana = new Habitat(seresVivos = [pepita, palmera, arbusto])
	
	const vaca1 = new Animal(especie = ganado, peso = 100, nivelDeHidratacion=50)
	const vaca2 = new Animal(especie = ganado, peso = 100, nivelDeHidratacion=50)
	const vaca3 = new Animal(especie = ganado, peso = 100, nivelDeHidratacion=50)
	
	const granjaDeVacas = new Granja(seresVivos =  [vaca1, vaca2, vaca3])
		
	test "Se puede saber si la granja tiene una especie"{
		assert.that(granja.tieneEspecie(plantusPotus))	
	}
		
	test "Las sabanas no entienden el metodo sacarSerVivo"{
		assert.throwsException({sabana.sacarSerVivo()})
	}
	 
	test "La granja de vacas esta en equilibrio" { 
		assert.that(granjaDeVacas.estaEnEquilibrio())	
	}
	
	test "Una granja de vacas despues de un incendio esta lista para dar comida"{
		granjaDeVacas.incendio()
		assert.that(granjaDeVacas.estaLista())	
	}		
}

describe "Cuevas"{
	const hormiga = new Especie(coeficiente = 30, formaLocomocion = correr, pesoReferencia = 0.1)
	const atomica1 = new Animal(especie = hormiga, peso = 0.01)
	const atomica2 = new Animal(especie = hormiga, peso = 0.01)
	const atomica3 = new Animal(especie = hormiga, peso = 0.01) 
	
	const hongo = new Especie()
	const champi1 = new Planta(altura = 1, especie = hongo)
	const champi2 = new Planta(altura = 1, especie = hongo)
	const champi3 = new Planta(altura = 1, especie = hongo)
	const champi4 = new Planta(altura = 1, especie = hongo)
	const champi5 = new Planta(altura = 1, especie = hongo)
	
	const cueva = new Cueva(seresVivos = [atomica1, atomica2, atomica3, champi1, champi2, champi3, champi4, champi5])
	const bosque = new Habitat(seresVivos = [atomica1, atomica2, atomica3, champi1, champi2, champi3, champi4, champi5])
		
	test "Una cueva tiene une biomasa menor que un bosque con los mismos seres vivos"{
		assert.that(bosque.biomasa() > cueva.biomasa())	
	}
}


describe "Seres vivos"{
	const golondrina = new Especie(coeficiente = 15, formaLocomocion = volar, pesoReferencia = 2)
	const ganado = new Especie(coeficiente = 24, pesoReferencia = 0.5)  
	
	const vaca = new Animal(especie = ganado, peso = 100, nivelDeHidratacion=50)
	
	test "Una vaca al tomarAgua sube en 10 su hidratacion"{
		assert.equals(50, vaca.nivelDeHidratacion())	
		
		vaca.tomarAguaMarciana()
		
		assert.equals(60, vaca.nivelDeHidratacion())	
	}
	
	test "Una vaca esta lista para ser comida"{
		vaca.sufrirIncendio()
		
		assert.that(vaca.estaListo())
	}
}

